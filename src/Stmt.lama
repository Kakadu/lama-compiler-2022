-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun evalRead (st, world, v) {
  var res;
  res := readWorld (world);
  [st <- [v, res[0]], res[1]]
}

fun evalWrite (st, world, expr) {
  [st, writeWorld (evalExpr (st, expr), world)]
}

fun eval (c, stmt) {
  var st, world;
  st := c[0];
  world := c[1];
  case stmt of
    Assn (v, expr) -> [st <- [v, evalExpr (st, expr)], world]
  | Seq (stmt1, stmt2) -> eval (eval (c, stmt1), stmt2)
  | Skip -> c
  | Read (v) -> evalRead (st, world, v)
  | Write (expr) -> evalWrite (st, world, expr)
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
