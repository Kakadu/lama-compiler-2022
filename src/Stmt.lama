-- Statement evaluator.

import List;
import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn   (string, expr)     |
--        Seq    (stmt, stmt)       |
--        Skip                      |
--        Read   (string)           |
--        Write  (expr)             |

fun eval (c @ [st, w], stmt) {
  case stmt of 
    Skip -> c
  | Assn (string, expr) -> [st <- [string, evalExpr(st, expr)], w]
  | Read (string) -> case readWorld(w) of [n, nw] -> [st <- [string, n], nw] esac 
  | Write (expr) -> [st, writeWorld(evalExpr(st, expr), w)]
  | Seq (stmt1, stmt2) -> eval(eval(c, stmt1), stmt2)
  | If (expr, stmt1, stmt2) -> if evalExpr(st, expr) then eval(c, stmt1) else eval(c, stmt2) fi
  | While (expr, stmt0) -> if evalExpr(st, expr) then eval(eval(c, stmt0), stmt) else c fi
  | DoWhile (stmt0, expr) ->
    case eval(c, stmt0) of
      nc@[st, w] -> if evalExpr(st, expr) then eval(nc, stmt) else nc fi
    esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
