-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval(c@[state, world], stmt) {
  case stmt of
    Assn (string, expr)       -> [state <- [string, evalExpr(state, expr)], world]
    | Seq (stmt1, stmt2)      -> eval(eval(c, stmt1), stmt2)
    | Skip                    -> c
    | Read (string)           -> case readWorld(world) of [value, nwrld] -> [state <- [string, value], nwrld] esac
    | Write (expr)            -> [state, writeWorld(evalExpr(state, expr), world)]
    | If (expr, stmt1, stmt2) -> if evalExpr(state, expr) then eval(c, stmt1) else eval(c, stmt2) fi
    | While (expr, stmt)      ->
      while case c of [state, _] -> evalExpr(state, expr) esac
      do
        c := eval(c, stmt)
      od;
      c
    | DoWhile (stmt, expr)    ->
      do
        c := eval(c, stmt) while case c of [state, _] -> evalExpr(state, expr) esac
      od;
      c
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
