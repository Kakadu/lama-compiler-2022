-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c@[state, world], stmt) {
  case stmt of
    Assn (s, expr) -> [state <- [s, evalExpr (state, expr)], world]
  | Seq (lhs, rhs) -> c := eval (eval (c, lhs), rhs)
  | Skip           -> c
  | Read (s)       -> case readWorld (world) of [v, w] -> [state <- [s, v], w] esac
  | Write (expr)   -> [state, writeWorld (evalExpr (state, expr), world)]
  | If (e, s1, s2) -> if evalExpr(state, e) then eval(c, s1) else eval(c, s2) fi
  | While (e, s)   -> if evalExpr(state, e) then eval(eval(c, s), stmt) else c fi
  | DoWhile (s, e) -> eval(eval(c, s), While (e, s))
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
