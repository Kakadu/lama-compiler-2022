-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun evalWhile(c@[s, w], e, stmt) {
  if evalExpr(s, e) then evalWhile(eval(c, stmt), e, stmt) else c fi
}

fun eval (c@[s, w], stmt) {
  case stmt of
    Assn (variable, e) -> [s <- [variable, evalExpr(s, e)], w]
  | Seq (stmt1, stmt2) -> eval(eval(c, stmt1), stmt2)
  | Skip -> c
  | Read (variable) -> case readWorld(w) of [v, w] -> [s <- [variable, v], w] esac
  | Write (e) -> [s, writeWorld(evalExpr(s, e), w)]
  | If (e, stmt1, stmt2) -> if evalExpr(s, e) then eval(c, stmt1) else eval(c, stmt2) fi
  | While (e, stmt1) -> evalWhile(c, e, stmt1)
  | DoWhile (stmt1, e) -> evalWhile(eval(c, stmt1), e, stmt1)
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}