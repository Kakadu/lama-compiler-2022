-- Parser 

import Ostap;
import Lexer;
import List;
import Fun;
import Matcher;
import Ref;
import Collection;

-- Signals an error; takes an error message and location info
fun error (msg, loc) {
  failure ("%s at %d:%d\n", msg, loc.fst, loc.snd)
}

-- An attribute-processing functions
-- Attributes are:
--   Val  --- a plain value (aka "rvalue")
--   Ref  --- a reference to a mutable cell (aka "lvalue")
--   Void --- no value (aka "void"/"unit")

-- Checks if a plain value "val" can be used in the context described by
-- the attribute "atr".
fun assertValue (atr, v, loc) {
  case atr of
    Ref  -> error ("reference expected", loc)
  | Void -> Ignore (v)
  | _    -> v
  esac
}

fun assertVoid (atr, v, loc) {
  case atr of
    Void -> v
  | Val  -> error ("value expected", loc)
  | _    -> error ("reference expected", loc)
  esac
}

-- A parser of "something" in brackets; l, r are left and right
-- brackets as parsers, p --- a parser of "something"
fun inbr (l, p, r) {
  syntax (-l p -r)
}

fun binop (op) {
  [syntax (pos -s[op]), fun (l, loc, r) {
                          fun (a) {
                            assertValue (a, Binop (op, l (Val), r (Val)), loc)
                          }
                        }
  ]
}

var ifp = memo $ eta syntax (
      loc=pos kIf cond=exp kThen sTrue=exp kFi                      {fun (a) {assertVoid(a, If(cond(Val), sTrue(Void), Skip), loc)}}   |
              kIf cond=exp kThen sTrue=exp kElse sFalse=exp kFi     {fun (a) {If(cond(Val), sTrue(a), sFalse(a))}} |
              kIf cond=exp kThen sTrue=exp rest=elifsp              {fun (a) {If(cond(Val), sTrue(a), rest(a))}}),
    elifsp = memo $ eta syntax (
              kElif cond=exp kThen sTrue=exp rest=elifsp            {fun (a) {If(cond(Val), sTrue(a), rest(a))}}   |
              kElif cond=exp kThen sTrue=exp kElse sFalse=exp kFi   {fun (a) {If(cond(Val), sTrue(a), sFalse(a))}} |
      loc=pos kElif cond=exp kThen sTrue=exp kFi                    {fun (a) {assertVoid(a, If(cond(Val), sTrue(Void), Skip), loc)}}),
    whilep = memo $ eta syntax (
      loc=pos kWhile cond=exp kDo body=exp kOd                      {fun (a) {assertVoid(a, While(cond(Val), body(Void)), loc)}}),
    dowhilep = memo $ eta syntax (
      loc=pos kDo body=exp kWhile cond=exp kOd                      {fun (a) {assertVoid(a, DoWhile(body(Void), cond(Val)))}}),
    forp = memo $ eta syntax (
      loc=pos kFor init=exp s[","] cond=exp s[","] iter=exp kDo body=exp kOd
                                                                    {fun (a) {assertVoid(a, Seq(init(Void), While(cond(Val), Seq(body(Void), iter(Void)))), loc)}}),
    readp = memo $ eta syntax (
      loc=pos kRead s["("] x=lident s[")"]                          {fun (a) {assertVoid(a, Read(x), loc)}}),
    writep = memo $ eta syntax (
      loc=pos kWrite s["("] e=exp s[")"]                            {fun (a) {assertVoid(a, Write(e(Val)), loc)}}),
    skipp = memo $ eta syntax (
      loc=pos kSkip                                                 {fun (a) {assertVoid(a, Skip, loc)}}),
    primary  = memo $ eta syntax (                   
                                  -- decimal constant
                                  loc=pos x=decimal                                 {fun (a) {assertValue (a, Const (stringInt (x)), loc)}}     |

                                  -- identifier
                                  x=lident                                          {fun (a) {
                                                                                       case a of
                                                                                         Ref  -> Ref (x)
                                                                                       | Void -> Ignore (Var (x))
                                                                                       | _    -> Var (x)
                                                                                       esac
                                                                                    }} |
                                  inbr[s("("), exp, s(")")] | ifp | whilep | dowhilep | forp | readp | writep | skipp),
    basic    = memo $ eta (expr ({[Right, {[s (":="),
                                            fun (l, loc, r) {
                                              fun (a) {assertValue (a, Assn (l (Ref), r (Val)), loc)}
                                            }]}],
                                  [Left , map (binop, {"!!"})],
                                  [Left , map (binop, {"&&"})],
                                  [Nona , map (binop, {"==", "!=", "<", ">", "<=", ">="})],
                                  [Left , map (binop, {"+", "-"})],
                                  [Left , map (binop, {"*", "/", "%"})]
                                 },
                                 primary) | primary),
    exp      = memo $ eta syntax (basic | s1=basic s[";"] s2=exp {fun (a) {Seq (s1 (Void), s2 (a))}});

-- Public top-level parser
public parse = syntax (s=exp {s (Void)});
