-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Assn (name, value) -> 
      case evalList (c, {name, value}) of
        [[s, w], {Ref (x), Val (v)}] -> [[s <- [x, v], w], Val (v)]
      esac
  | Seq (stmt1, stmt2) -> 
      case evalList (c, {stmt1, stmt2}) of
        [c, {_, v}] -> [c, v]
      esac   
  | Skip -> [c, Void]
  | Read (name) ->
      case readWorld (w) of
        [v, w] -> [[s <- [name, v], w], Void]
      esac  
  | Write (e) -> 
      case eval (c, e) of
        [[s, w], Val (v)] -> [[s, writeWorld (v, w)], Void]
      esac  
  | If (cond, stmt1, stmt2) -> 
      case eval (c, cond) of 
        [c, Val (0)] -> eval (c, stmt2)
      | [c, _] -> eval (c, stmt1)
      esac
  | While (cond, body) -> 
      case eval (c, cond) of
        [c, Val (0)] -> [c, Void]
      | [c, _] -> case eval (c, body) of [c, _] -> eval (c, expr) esac
      esac
  | DoWhile (body, cond) -> 
      case eval (c, body) of
        [c, _] -> eval (c, While (cond, body))
      esac  
  | Var (x) -> [c, Val (s (x))]
  | Ref (x) -> [c, Ref (x)]
  | Const (n) -> [c, Val (n)]
  | Binop (op, x, y) -> 
      case evalList (c, {x, y}) of
        [cc, { Val (l), Val (r)}] -> [cc, Val (evalOp (op, l, r))]
      esac
  | Ignore (e) -> 
      case eval (c, e) of
        [c, _] -> [c, Void]
      esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
