-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+", infix +],
  ["-", infix -],
  ["*", infix *],
  ["/", infix /],
  ["%", infix %],
  ["==", infix ==],
  ["!=", infix !=],
  ["<", infix <],
  ["<=", infix <=],
  [">", infix >],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp(op, l, r) {
  case assoc(ops, op) of
    Some (f) -> f(l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar(state, name) {
  case state.lookup(name) of
    Var (_) -> skip
    | _     -> error(sprintf("the name ""%s"" does not designate a variable", name), getLoc(name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun(state, name) {
  case state.lookup(name) of
    Fun (_, _) -> skip
    | _        -> error(sprintf("the name ""%s"" does not designate a function", name), getLoc(name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames(state, names) {
  foldl(fun (s, name) { s.addName(name, Var(0)) }, state, names)
}

-- Helper function: adds a function in current scope
fun addFunction(state, name, args, body) {
  state.addName(name, Fun(args, body))
}

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList(c, exprs) {
  case foldl(fun ([c, vals], e) {
    case eval(c, e) of
      [c, v] -> [c, v:vals]
    esac
  },
    [c, { }],
    exprs) of
    [c, vals] -> [c, reverse(vals)]
  esac
}

fun eval(c@[s, w], expr) {
  case expr of
    Assn (l, r)            ->
      case evalList(c, { l, r }) of
        [[s, w], { Ref (x), v }] -> [[s <- [x, Var(v)], w], v]
      esac
    | Seq (s1, s2)         ->
      case evalList(c, { s1, s2 }) of
        [c, { _, v }] -> [c, v]
      esac
    | Skip                 -> [c, None]
    | Read (x)             ->
      checkVar(s, x);
      case readWorld(w) of [v, w] ->
        [[s <- [x, Var(v)], w], None]
      esac
    | Write (e)               ->
      case eval(c, e) of [[s, w], v] ->
        [[s, writeWorld(v, w)], None]
      esac
    | If (cond, iBody, eBody) ->
      case eval(c, cond) of [c, b] ->
        if b then
          eval(c, iBody)
          else
            eval(c, eBody)
        fi
      esac
    | While (cond, body)      ->
      case eval(c, cond) of
        [c, cond] ->
          if cond then
            case eval(c, body) of [c, None] ->
              eval(c, expr)
            esac
            else [c, None]
          fi
      esac
    | DoWhile (body, cond) ->
      case eval(c, body) of
        [c, None] -> eval(c, While(cond, body))
      esac
    | Var (x)              -> checkVar(s, x); [c, case lookup(s, x) of Var(v) -> v esac]
    | Ref (x)              -> checkVar(s, x); [c, Ref(x)]
    | Const (n)            -> [c, n]
    | Binop  (op, e1, e2)  -> case evalList(c, { e1, e2 }) of
      [c, { e1, e2 }] -> [c, evalOp(op, e1, e2)]
    esac
    | Ignore (e)           -> case eval(c, e) of [c, _] -> [c, None] esac
    | Scope (ds, e)        ->
      case eval([foldl(fun (s, def) { case def of
        Var (names)                -> addNames(s, names)
        | Fun (name, params, body) -> addFunction(s, name, params, body)
      esac
      }, enterScope(s), ds), w], e) of
        [[s, w], v] -> [[leaveScope(s), w], v]
      esac
    | Call (f, args)       ->
      s.checkFun(f);
      case s.lookup(f) of
        Fun (params, body) ->
          if size(args) != size(params) then
            error(sprintf("function ""%s"": %d arguments expected but %d found", f, params.size, args.size), getLoc(f))
          fi;
          case evalList(c, args) of
            [[s, w], args] ->
              case eval([foldl(fun (s, [param, arg]) { addName(s, param, Var(arg)) }, enterFunction(s), zip(params, args)), w], body) of
                [[s2, w], v] -> [[leaveFunction(s, getGlobal(s2)), w], v]
              esac
          esac
      esac
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalExpr(input, expr) {
  case eval([emptyState(), createWorld(input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}