-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Assn (l, r) ->
      case evalList (c, {l, r}) of
        [[s, w], { Ref (x), Val (v)}] -> [[s <- [x, v], w], Val (v)]
      esac
  | Seq (fst_e, snd_e) ->
      case evalList (c, {fst_e, snd_e}) of
        [c, {_, v}] -> [c, v]
      esac
  | Skip -> [c, Void]
  | Read (x) -> 
      case readWorld (w) of
        [new_x, w] -> [[s <- [x, new_x], w], Void]
      esac 
  | Write (e) ->
      case eval (c, e) of
        [[s, w], Val (n)] -> [[s, writeWorld (n, w)], Void]
      esac
  | If (cond, then_e, else_e) ->
      case eval (c, cond) of
        [c, Val (n)] -> if n then eval (c, then_e) else eval (c, else_e) fi
      esac
  | While (cond, body) -> 
      case eval (c, cond) of 
        [c, Val (n)] -> if n then eval (eval (c, body)[0], expr) else [c, Void] fi 
      esac
  | DoWhile (body, cond) -> var one_iter = eval (c, body)[0]; eval (one_iter, While (cond, body))
  | Var (x) -> [c, Val (s (x))]
  | Ref (x) -> [c, Ref (x)]
  | Const (n) -> [c, Val (n)]
  | Binop (op, l, r) ->
      case evalList (c, {l, r}) of
        [c, { Val (l), Val (r)}] -> [c, Val (evalOp (op, l, r))]
      esac 
  | Ignore (e) -> [eval(c, e)[0], Void]
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
