-- Stack machine.

import List;
import World;
import State;
import Expr;
import Buffer;
import Fun;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ      -> sprintf ("READ")
  | WRITE     -> sprintf ("WRITE")
  | BINOP (s) -> sprintf ("BINOP %s", s)
  | LD    (x) -> sprintf ("LD %s", x)
  | ST    (x) -> sprintf ("ST %s", x)
  | CONST (n) -> sprintf ("CONST %d", n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

fun evalOneI (c@[stack, state, world], i) {
  case i of
    READ      -> case readWorld(world) of [n, world] -> [n:stack, state, world] esac
  | WRITE     -> case stack of n:stack -> [stack, state, writeWorld(n, world)] esac
  | BINOP (s) -> case stack of y:x:stack -> [evalBinOp(s, x, y):stack, state, world] esac
  | LD    (x) -> [state(x):stack, state, world]
  | ST    (x) -> case stack of n:stack -> [stack, state <- [x, n], world] esac
  | CONST (n) -> [n:stack, state, world]
  esac
}

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (c, insns) {
  case insns of
    {} -> c
  | curI:insns -> eval(evalOneI(c, curI), insns)
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval ([{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Var (v) -> {LD (v)}
  | Const (x) -> {CONST (x)}
  | Binop (op, e1, e2) -> compileExpr(e1) +++ compileExpr(e2) +++ {BINOP (op)}
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  case stmt of
    Skip -> {}
  | Seq (s1, s2) -> compileSM(s1) +++ compileSM(s2)
  | Assn (v, e) -> compileExpr(e) +++ {ST (v)}
  | Read (v) -> {READ, ST (v)}
  | Write (e) -> compileExpr(e) +++ {WRITE}
  esac
}
