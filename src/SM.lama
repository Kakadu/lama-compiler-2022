-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Evaluate binop. Takes string representation of the binop and operands.
fun evalBinop(binop, l, r) {
  case binop of
    "+" -> l + r
  | "-" -> l - r
  | "*" -> l * r
  | "/" -> l / r
  | "%" -> l % r
  | "<" -> l < r
  | ">" -> l > r
  | "<=" -> l <= r
  | ">=" -> l >= r
  | "==" -> l == r
  | "!=" -> l != r
  | "&&" -> l && r
  | "!!" -> l !! r
  esac
}

fun condSatisfied (value, suf) {
  case [suf, value] of
    ["z",  0] -> true
  | ["z",  _] -> false
  | ["nz", 0] -> false
  | ["nz", _] -> true
  esac
}

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c@[stack, state, w], insns) {
  case insns of
    {} -> c
  | ins : insns ->
      case ins of
        READ         -> case readWorld(w) of [v, w] -> eval(env, [v : stack, state, w], insns) esac
      | WRITE        -> case stack of v : stack -> eval(env, [stack, state, writeWorld(v, w)], insns) esac
      | BINOP (s)    -> case stack of r : l : stack -> eval(env, [evalBinop(s, l, r) : stack, state, w], insns) esac
      | LD    (x)    -> eval(env, [state(x) : stack, state, w], insns)
      | ST    (x)    -> case stack of v : stack -> eval(env, [stack, state <- [x, v], w], insns) esac
      | CONST (n)    -> eval(env, [n : stack, state, w], insns)
      | LABEL (s)    -> eval(env, c, insns)
      | JMP   (l)    -> eval(env, c, fromLabel(env, l))
      | CJMP  (s, l) ->
          case stack of v : stack ->
            if condSatisfied(v, s) then
              eval(env, [stack, state, w], fromLabel(env, l))
            else
              eval(env, [stack, state, w], insns)
            fi
          esac
      esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Var (v) -> singletonBuffer(LD (v))
  | Const (num) -> singletonBuffer(CONST (num))
  | Binop (binop, e1, e2) -> compileExpr(e1) <+> compileExpr(e2) <+ BINOP (binop)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If (e, s1, s2) ->
        case env.genLabel of [s1End, env] ->
          case env.genLabel of [s2Start, env] ->
            case compile (s1End, env, s1) of [s1EndUsed, env, s1Code] ->
              case compile (lab, env, s2) of [_, env, s2Code] ->
                [true, env, compileExpr(e) <+ CJMP ("z", s2Start) <+> s1Code <+> label(s1End, s1EndUsed)
                            <+ JMP(lab) <+ LABEL(s2Start) <+> s2Code]
              esac
            esac
          esac
        esac

    | While (e, s) ->
        case env.genLabel of [startLab, env] ->
          case env.genLabel of [condLab, env] ->
            case compile(condLab, env, s) of [_, env, sCode] ->
              [false, env, listBuffer({JMP (condLab), LABEL (startLab)}) <+> sCode <+ LABEL (condLab)
                           <+> compileExpr(e)  <+ CJMP ("nz", startLab)]

            esac
          esac
        esac
    | DoWhile (s, e) ->
        case env.genLabel of [bodyLab, env] ->
          case env.genLabel of [sEnd, env] ->
            case compile(sEnd, env, s) of [sEndUsed, env, sCode] ->
              [false, env, singletonBuffer(LABEL (bodyLab)) <+> sCode <+> label(sEnd, sEndUsed)
                           <+> compileExpr(e) <+ CJMP ("nz", bodyLab)]
            esac
          esac
        esac
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
