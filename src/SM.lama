-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {
  var stack = 0;
  var state = 1;
  var world = 2;

  fun pop () {
    case c[stack] of hd : tl -> c[stack] := tl; hd esac
  }

  fun push (v) {
    c[stack] := {v} +++ c[stack]
  }

  fun evalSingle (ins) {
    case ins of
      READ      -> case readWorld (c[world]) of [v, w] -> c[world] := w; push (v) esac
    | WRITE     -> c[world] := writeWorld (pop (), c[world])
    | BINOP (s) -> var rhs = pop (); push (evalBinOp (s, pop (), rhs))
    | LD (x)    -> push (c[state] (x))
    | ST (x)    -> c[state] := c[state] <- [x, pop ()]
    | CONST (n) -> push (n)
    esac
  }

  case insns of
    {} -> c
  | JMP (l) : _ -> eval (env, c, env.fromLabel (l))
  | CJMP (cond, l) : tl ->
      case c[stack] of n : stackTail ->
        c[stack] := stackTail;
        if (compare(cond, "z") == 0 && n == 0) !! (compare(cond, "nz") == 0 && n != 0)
        then eval (env, c, env.fromLabel (l))
        else eval (env, c, tl)
        fi
      esac
  | LABEL (_) : tl -> eval (env, c, tl)
  | hd : tl -> evalSingle (hd); eval (env, c, tl)
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Const (value) -> singletonBuffer (CONST (value))
  | Var (name) -> singletonBuffer (LD (name))
  | Binop (op, lhs, rhs) -> compileExpr (lhs) <+> compileExpr (rhs) <+ BINOP (op)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If (e, s1, s2) ->
        case env.genLabels (2) of
          [s1Lab, elseLab, env] ->
            case compile (s1Lab, env, s1) of
              [s1LabUsed, env, s1Code] ->
                case compile (lab, env, s2) of
                  [labUsed, env, s2Code] ->
                    var code = compileExpr (e) <+ CJMP ("z", elseLab)
                               <+> s1Code <+> label (s1Lab, s1LabUsed)
                               <+ JMP (lab) <+ LABEL (elseLab) <+> s2Code;
                    [true, env, code]
                esac
            esac
        esac
    | While (e, s) ->
        case env.genLabels (2) of
          [exprLab, stmtLab, env] ->
            case compile(stmtLab, env, s) of
              [stmtLabUsed, env, sCode] ->
                var code = singletonBuffer (LABEL (exprLab)) <+> compileExpr (e)
                           <+ CJMP ("z", lab) <+> sCode <+> label ( stmtLab, stmtLabUsed)
                           <+ JMP (exprLab);
                [true, env, code]
            esac
        esac
    | DoWhile (s, e) ->
      case env.genLabels (2) of
        [stmtLab, eLab, env] ->
          case compile (stmtLab, env, s) of
            [stmtLabUsed, env, sCode] ->
              var code = singletonBuffer (LABEL (eLab)) <+> sCode
                        <+> label (stmtLab, stmtLabUsed) <+> compileExpr (e)
                        <+ CJMP("nz", eLab);
              [false, env, code]
          esac
      esac
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
