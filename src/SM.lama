-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;


fun binop(b, v1, v2) {
  case b of
    "+"  -> v1 +  v2
  | "-"  -> v1 -  v2
  | "*"  -> v1 *  v2
  | "/"  -> v1 /  v2
  | "%"  -> v1 %  v2
  | "<"  -> v1 <  v2
  | ">"  -> v1 >  v2
  | "<=" -> v1 <= v2
  | ">=" -> v1 >= v2
  | "==" -> v1 == v2
  | "!=" -> v1 != v2
  | "&&" -> v1 && v2
  | "!!" -> v1 !! v2
  esac
}

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

fun execSMIns(c, i) {
  case c of [stack, st, w] ->
    case i of
      READ      -> case readWorld(w) of [v, w] -> [v : stack, st, w] esac
    | WRITE     -> case stack of v : stack -> [stack, st, writeWorld(v, w)] esac
    | BINOP (s) -> case stack of v2 : v1 : stack -> [binop(s, v1, v2) : stack, st, w] esac
    | LD    (x) -> [st(x) : stack, st, w]
    | ST    (x) -> case stack of v : stack -> [stack, st <- [x, v], w] esac
    | CONST (n) -> [n : stack, st, w]
    | LABEL (_) -> c
    esac
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

fun insType (i) {
  case i of
    READ      -> NonJumping
  | WRITE     -> NonJumping
  | BINOP (_) -> NonJumping
  | LD    (_) -> NonJumping
  | CONST (_) -> NonJumping
  | ST    (_) -> NonJumping
  | LABEL (_) -> NonJumping
  | _         -> Jumping
  esac
}

fun insnsAfterCondJump(env, value, cond, lab, oldInsns) {
  if (compare(cond, "z") == 0 && value == 0) !! (compare(cond, "nz") == 0 && value) then
    fromLabel(env, lab)
  else
    oldInsns
  fi
}

fun confStackPop([v : stack, st, w]) {
  [v, [stack, st, w]]
}

fun printInsns(insns) {
  case insns of
    {} -> skip
  | i : insns -> printf ("%s\n", i.string); printInsns(insns)
  esac
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c, insns) {
--  if cnt == 0 then printInsns(insns); cnt := 1 fi;
  case insns of
    {}        -> c
  | i : insns ->
      case insType(i) of
        NonJumping -> eval(env, execSMIns(c, i), insns)
      | Jumping    ->
          case i of
            JMP  (l)    -> eval(env, c, fromLabel(env, l))
          | CJMP (s, l) ->
              case confStackPop(c) of [v, c] ->
                eval(env, c, insnsAfterCondJump(env, v, s, l, insns))
              esac
          esac
      esac
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Var (v)           -> singletonBuffer(LD (v))
  | Const (c)         -> singletonBuffer(CONST (c))
  | Binop (b, e1, e2) -> compileExpr(e1) <+> compileExpr(e2) <+ BINOP (b)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If (cond, sTrue, sFalse) ->
        case env.genLabels(2) of [sTrueEnd, sFalseStart, env] ->
          case compile (sTrueEnd, env, sTrue) of [sTrueEndUsed, env, sTrueCode] ->
            case compile (lab, env, sFalse) of [labUsed, env, sFalseCode] ->
              [true, env, compileExpr(cond)
                          <+  CJMP ("z", sFalseStart)
                          <+> sTrueCode
                          <+> label(sTrueEnd, sTrueEndUsed)
                          <+  JMP(lab)
                          <+  LABEL(sFalseStart)
                          <+> sFalseCode]
            esac
          esac
        esac

    | While (cond, body) ->
        case env.genLabels(2) of [condStart, bodyEnd, env] ->
          case compile(bodyEnd, env, body) of [bodyEndUsed, env, bodyCode] ->
            [true, env, singletonBuffer(LABEL(condStart))
                        <+> compileExpr(cond)
                        <+  CJMP("z", lab)
                        <+> bodyCode
                        <+> label(bodyEnd, bodyEndUsed)
                        <+  JMP(condStart)]

          esac
        esac
    | DoWhile (body, cond) ->
        case env.genLabels(2) of [bodyStart, bodyEnd, env] ->
          case compile(bodyEnd, env, body) of [bodyEndUsed, env, bodyCode] ->
            [false, env, singletonBuffer(LABEL(bodyStart))
                         <+> bodyCode
                         <+> label(bodyEnd, bodyEndUsed)
                         <+> compileExpr(cond)
                         <+  CJMP ("nz", bodyStart)]
          esac
        esac
    | _              -> failure ("compileSM not implemented\n")
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}

var cnt = 0;
