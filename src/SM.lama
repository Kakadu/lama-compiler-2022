-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, [stack, state, world], insns) {
  case insns of 
     cmd : insns ->
        case cmd of 
            CJMP  (c, l) -> case c of 
              "z" ->
              case stack of
                h : tl -> if h == 0 then 
                  eval(env, [tl, state, world], env.fromLabel(l))
                else 
                  eval(env, [tl, state, world], insns)
                fi 
              esac
            | "nz" ->
              case stack of
                h : tl -> if h != 0 then
                  eval(env, [tl, state, world], env.fromLabel(l))
                else 
                  eval(env, [tl, state, world], insns)
                fi 
              esac
            esac 
        | JMP (l) -> eval(env, [stack, state, world], env.fromLabel(l))
        | _ -> case evalCmd([env, stack, state, world], cmd) of 
                  [env, stack, state, world] -> eval(env, [stack, state, world], insns)
               esac
        esac
    | {} -> [env, stack, state, world]
  esac
}

fun evalBinOp (a, b, op) {
  case op of 
     "*"  -> a *  b
  |  "+"  -> a +  b 
  |  "-"  -> a -  b
  |  "/"  -> a /  b
  |  "%"  -> a %  b
  |  "<"  -> a <  b
  |  ">"  -> a >  b 
  |  "<=" -> a <= b
  |  ">=" -> a >= b
  |  "&&" -> a && b
  |  "!!" -> a !! b
  |  "==" -> a == b
  |  "!=" -> a != b
  esac
}

fun evalCmd ([env, stack, state, world], cmd) {
  case cmd of
    READ         -> case readWorld(world) of [n, w] -> [env, {n} +++ stack, state, w] esac
  | WRITE        -> [env, stack.tl, state, writeWorld(stack.hd, world)]
  | BINOP (s)    -> [env, {evalBinOp(stack.tl.hd, stack.hd, s)} +++ stack.tl.tl, state, world]
  | LD    (x)    -> [env, {state(x)} +++ stack, state, world]
  | ST    (x)    -> [env, stack.tl, state <- [x, stack.hd], world]
  | CONST (n)    -> [env, {n} +++ stack, state, world]
  | LABEL (s)    -> [env, stack, state, world]
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[3].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
      Var (s) -> singletonBuffer (LD (s))
    | Const (i) -> singletonBuffer (CONST (i))
    | Binop (s, exp1, exp2) -> compileExpr(exp1) <+> compileExpr(exp2) <+ BINOP (s)
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | While (e, s) -> case env.genLabels (2) of
          [lab1, lab2, env] -> 
            case compile (lab2, env, s) of
              [lab2Used, env, code] -> 
                  [false, env, singletonBuffer (JMP (lab2)) <+ LABEL (lab1) <+> code <+ LABEL (lab2) <+> compileExpr(e) <+ CJMP ("nz", lab1)]
            esac
        esac
               
    | If (e, s1, s2) -> case env.genLabels (2) of
          [lab1, lab2, env] -> 
            case compile (lab2, env, s1) of
              [lab2Used, env, code1] -> 
                  case compile (lab2, env, s2) of
                    [lab2Used, env, code2] -> 
                        [false, env, compileExpr(e) <+ CJMP ("z", lab1) <+> code1 <+ JMP (lab2) <+ LABEL (lab1) <+> code2 <+ LABEL (lab2)]
              esac 
            esac
        esac
    | DoWhile (s, e) -> case env.genLabel of
          [lab1, env] ->
             case compile (lab1, env, s) of
                [labUsed, env, code] -> 
                    [false, env, singletonBuffer (LABEL (lab1)) <+> code <+> compileExpr(e) <+ CJMP ("nz", lab1)]
             esac
        esac
    | _ -> failure ("compileSM not implemented\n")
    esac
  }  
  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
